10   REM : STATISTICS I
20   REM : (PART 1)
40   CLS
50   PRINT" INTRODUCTION TO STATISTICS"
60   GOSUB9500
70   PRINT"MEAN/VARIANCE/STD. DEVIATION"
80   GOSUB9500
85   GOSUB9500
90   PRINT"OPTIONS :"
100  PRINT
110  PRINT"1. TUTORIAL SESSION"
120  PRINT"2. STATISTICAL PROCEDURE"
130  PRINT"3. SKIP TO NEXT TOPIC"
140  PRINT"4. END PACKAGE"
150  GOSUB9500
160  PRINT"YOUR CHOICE (1-4)";
170  INPUTO
180  IFO<=4ANDO=>1THEN200
190  GOTO160
200  CLS
210  IFO=4THEN3000
220  IFO=3THEN4000
230  IFO=2THENF=0
240  IFO=1THENGOSUB900
250  IFO=2THENGOSUB300
260  RUN
300  CLS
310  PRINT"MEAN/VARIANCE/STD. DEVIATION"
320  GOSUB9500
330  PRINT"WHICH METHOD"
340  PRINT"(0 = POPULATION)"
350  PRINT"(1 = SAMPLE)    ";
360  INPUTS
370  IFS<>0ANDS<>1THEN340
380  GOSUB9500
390  PRINT"KIND OF DATA"
400  PRINT"(0 = GROUPED)"
410  PRINT"(1 = UNGROUPED)";
420  INPUTK
430  IFK<>0ANDK<>1THEN400
440  GOSUB9500
450  PRINT"NO. OF OBSERVATION";
460  INPUTN
470  IFN<1THENGOSUB9500
480  IFN<1THENPRINT"N MUST => 1"
490  IFN<1THENGOSUB9500
500  IFN<1THEN450
510  R=0
520  M=0
530  P=0
535  GOSUB9500
540  IFK=1THENGOSUB600
550  IFK=0THENGOSUB700
555  GOSUB9500
560  GOSUB800
570  RETURN
600  FORI=1TON
610  IFF=0THENPRINT"ITEM   ";I;
620  IFF=1THENPRINT"PERSON ";I;" AGED ";
630  INPUTD
640  P=P+D
650  M=M+D**2
660  NEXT
670  R=P/N
680  V=(M-N*R**2)/(N-S)
690  RETURN
700  FORI=1TON
710  IFF=0THENPRINT"ITEM, FREQUENCY ";I;
720  IFF=1THENPRINT"AGE, QUANTITY ";I;
730  INPUTA,B
740  R=R+B*A
750  P=P+B
760  M=M+B*A**2
770  NEXT
780  R=R/P
790  V=(M-P*R**2)/(P-S)
795  RETURN
800  IFF=1ANDS=0THENPRINT"FOR GROUPED POPULATED DATA :"
810  IFF=1ANDS=1THENPRINT"FOR UNGROUPED SAMPLE DATA :"
820  GOSUB9500
830  PRINT"MEAN : ";R
840  PRINT"VARIANCE : ";V
845  IFV=0THEN853
850  PRINT"STD. DEVIATION : ";SQR(V)
852  IFV<>0THEN855
853  PRINT"STD. DEVIATION : ";0
855  GOSUB9500
856  GOSUB9000
860  RETURN
900  CLS
910  PRINTTAB(7);"TUTORIAL SESSION"
920  GOSUB9500
930  PRINT"MEAN : IS THE TOTAL OF THE"
940  PRINT"VALUES OF A SET OF OBSER-"
950  PRINT"VATIONS DIVIDED BY THE NO."
960  PRINT"OF OBSERVATIONS. IT MEASURES"
970  PRINT"CENTRAL TENDENCY OF A POP-"
980  PRINT"ULATION."
990  GOSUB9500
1000 GOSUB9000
1010 PRINT"FOR UNGROUPED DATA :"
1020 PRINT
1030 PRINT"MEAN = (X1+X2+...+XN)/N"
1035 PRINT
1040 PRINT"X = VALUE OF OBSERVATION"
1050 PRINT"N = NO. OF OBSERVATIONS"
1060 GOSUB9500
1070 PRINT"FOR GROUPED DATA :"
1080 PRINT
1090 PRINT"MEAN = (F1M1+F2M2+...+FNMN)"
1100 PRINT"       --------------------"
1110 PRINT"                 N"
1115 PRINT
1120 PRINT"M = MIDPOINT OF A CLASS"
1130 PRINT"F = FREQUENCY OF THE CLASS"
1140 PRINT"N = TOTAL NO. OF FREQUENCY"
1150 PRINT"  = (F1+F2+...+FN)"
1160 GOSUB9000
1170 PRINTTAB(7);"TUTORIAL SESSION"
1180 GOSUB9500
1190 PRINT"VARIANCE : IS THE ARITHMETIC"
1200 PRINT"MEAN OF SQUARED DEVIATIONS"
1210 PRINT"FROM THE POPULATION MEAN. IT"
1220 PRINT"MEASURES THE EXTENT OF VAR-"
1230 PRINT"IATION IN THE VALUES OBSER-"
1240 PRINT"VATIONS FROM CENTRAL TENDENCY."
1250 GOSUB9500
1260 GOSUB9000
1270 PRINT"FOR UNGROUPED DATA :"
1280 GOSUB9500
1290 PRINT"VAR. = (X**2-N(M**2))/(N-S)"
1295 PRINT
1300 PRINT"X = X1+X2+...+XN"
1310 PRINT"N = NO. OF OBSERVATIONS"
1320 PRINT"M = ARITHMETIC MEAN"
1330 PRINT"S = 0 FOR POPULATION DATA"
1340 PRINT"  = 1 FOR SAMPLE DATA"
1350 GOSUB9500
1360 GOSUB9000
1370 PRINT"FOR GROUPED DATA :"
1380 GOSUB9500
1390 PRINT"VAR. = (FM**2-N(M**2))/(N-S)"
1395 PRINT
1400 PRINT"FM = F1M1+F2M2+...+FNMN"
1410 PRINT" N = F1+F2+...+FN"
1420 PRINT" M = ARITHMETIC MEAN"
1430 PRINT" S = 0 FOR POPULATION DATA"
1440 PRINT"   = 1 FOR SAMPLE DATA"
1450 GOSUB9500
1460 GOSUB9000
1470 PRINTTAB(7);"TUTORIAL SESSION"
1480 GOSUB9500
1490 PRINT"STANDARD DEVIATION : IS A"
1500 PRINT"MEASURE OF DISPERSION IN"
1510 PRINT"TERMS OF UNITS OF THE ORI-"
1520 PRINT"GINAL DATA.  IT IS USEFUL IN"
1530 PRINT"DESCRIBING THE GENERAL CHAR-"
1540 PRINT"ACTERISTICS OF DATA IN A"
1550 PRINT"FREQUENCY DISTRIBUTION AND"
1560 PRINT"HOW INDIVIDUAL ITEM DEVIATES"
1570 PRINT"FROM THE MEAN OF DISTRIBUT-"
1580 PRINT"ION."
1590 PRINT
1600 PRINT" STANDARD"
1610 PRINT"DEVIATION = SQR(VARIANCE)"
1620 GOSUB9500
1630 GOSUB9000
1640 PRINT"EG.1 : TO FIND MEAN, VAR-"
1650 PRINT"IANCE, AND STANDARD DEVI-"
1660 PRINT"ATION OF THE AGES OF ALL THE"
1670 PRINT"PEOPLE IN THE HOTEL BY USING"
1680 PRINT"THE PEOPLE IN THE LOBBY AS A"
1690 PRINT"SAMPLE."
1700 GOSUB9500
1710 PRINT"NO. OF PEOPLE IN LOBBY"
1715 PRINT"(MUST > 1) ";
1720 INPUTN
1730 GOSUB9500
1740 S=1
1750 K=1
1760 F=1
1770 R=0
1780 M=0
1790 P=0
1795 GOSUB9500
1800 GOSUB600
1805 GOSUB9500
1810 GOSUB800
1840 PRINT"EG.2 : TO FIND MEAN, VAR-"
1850 PRINT"IANCE, AND STANDARD DEVI-"
1860 PRINT"ATION OF THE AGES OF ALL"
1870 PRINT"CREAM CHEESE IN THE STORE."
1880 GOSUB9500
1890 PRINT"NO. OF OBSERVATIONS"
1900 PRINT"(MUST > 1) ";
1910 INPUTN
1920 GOSUB9500
1930 S=0
1940 K=0
1950 F=1
1960 R=0
1970 M=0
1980 P=0
1985 GOSUB9500
1990 GOSUB700
1995 GOSUB9500
2000 GOSUB800
2030 PRINTTAB(7);"TUTORIAL SESSION"
2040 GOSUB9500
2050 PRINT"REVIEW QUESTION :"
2060 PRINT"==============="
2070 GOSUB9500
2080 PRINT"WHAT IS THE STANDARD DEVI-"
2090 PRINT"ATION IF ALL VALUES IN A"
2100 PRINT"POPULATION ARE IDENTICAL ";
2110 INPUTX
2115 GOSUB9500
2120 IFX=0THENPRINT"YES, 0 IS CORRECT."
2130 IFX=0THEN2200
2140 PRINT"INCORRECT, REPEAT TUTORIAL !!"
2200 GOSUB9500
2210 GOSUB9000
2220 RETURN
3000 CLS
3010 PRINTTAB(8);"END OF PROGRAM"
3012 PRINT
3013 PRINT
3014 PRINT
3015 PRINTTAB(5);"PRESS RESET TO STOP"
3020 GOSUB9500
3030 GOSUB9500
3040 GOSUB9500
3100 GOTO3100
4000 CLS
4010 PRINTTAB(8);"TAPE LOADING"
4020 GOSUB9500
4025 GOSUB9500
4030 GOSUB9500
4040 GOSUB9500
4100 CRUN
9000 PRINT
9010 PRINT
9020 PRINT"HIT -RETURN- TO CONTINUE ";
9030 INPUTA$
9035 CLS
9040 RETURN
9500 PRINT
9505 PRINT
9510 PRINT
9520 RETURN
