10   REM : STATISTICS II
20   REM : (PART 3)
25   REM : MAIN PROGRAM
30   DIMP(20,2),V(2),D(2),R(2),M(2)
40   CLS
50   PRINT"INTRODUCTION TO STATISTICS II"
60   GOSUB9500
70   PRINTTAB(9);"T - TEST"
80   GOSUB9500
85   GOSUB9500
90   PRINT"OPTIONS :"
100  PRINT
110  PRINT"1. TUTORIAL SESSION"
120  PRINT"2. STATISTICAL PROCEDURE"
130  PRINT"3. SKIP TO NEXT TOPIC"
140  PRINT"4. END PACKAGE"
150  GOSUB9500
160  PRINT"YOUR CHOICE (1-4)";
170  INPUTO
180  IFO<=4ANDO=>1THEN200
190  GOTO160
200  CLS
210  IFO=4THEN4000
220  IFO=3THEN3000
230  IFO=2THENF=0
240  IFO=1THENGOSUB900
250  IFO=2THENGOSUB300
260  RUN
300  CLS
310  PRINTTAB(9);"T - TEST"
320  PRINT
322  PRINT
330  PRINT"TEST 1 :"
332  PRINT
333  PRINT"MEAN = GIVEN VALUE"
334  GOSUB9500
335  PRINT"TEST 2 :"
336  PRINT
340  PRINT"MEAN 1 = MEAN 2"
350  PRINT"STD. DEV. 1 = STD. DEV. 2"
355  GOSUB9500
360  PRINT"TEST 3 :"
365  PRINT
370  PRINT"MEAN 1 = MEAN 2"
380  PRINT"STD. DEV. 1 <> STD. DEV. 2"
385  GOSUB9000
390  PRINT"WHICH HYPOTHESIS"
392  PRINT"(1 - 3)";
394  INPUTT
395  GOSUB9500
396  GOSUB400
397  RETURN
400  IFT=1THENB=1
410  IFT<>1THENB=2
420  FORI=1TOB
430  V(I)=0
440  D(I)=0
450  PRINT"SAMPLE ";I;" :"
460  PRINT
470  PRINT"NO. OF ELEMENTS"
475  PRINT"(MUST > 2)";
480  INPUTR(I)
485  Z=R(I)
487  GOSUB9500
490  FORJ=1TOZ
500  PRINT"ELEMENT ";J;
510  INPUTP(J,I)
520  V(I)=V(I)+P(J,I)
530  D(I)=D(I)+P(J,I)**2
540  NEXTJ
545  GOSUB9500
550  M(I)=V(I)/R(I)
560  V(I)=D(I)-V(I)**2/R(I)
570  V(I)=V(I)/(R(I)-1)
580  NEXTI
590  GOSUB9500
600  IFT=2THEN680
610  IFT=3THEN745
620  PRINT"VALUE OF MEAN ";
630  INPUTM
640  Z=R(1)/V(1)
650  A=(M(1)-M)*SQR(Z)
660  B=R(1)-1
670  GOTO830
680  Z=1/R(1)+1/R(2)
690  A=(M(1)-M(2))/SQR(Z)
700  B=R(1)+R(2)-2
710  E=(R(1)-1)*V(1)
720  F=(R(2)-1)*V(2)
730  A=A/SQR((E+F)/B)
740  GOTO830
745  Z=V(1)/R(1)+V(2)/R(2)
750  A=(M(1)-M(2))/SQR(Z)
760  B=(V(1)/R(1)+V(2)/R(2))**2
770  E=(V(1)/R(1))**2
780  F=(V(2)/R(2))**2
790  G=R(1)+1
800  H=R(2)+2
810  B=B/(E/G+F/H)-2
820  B=INT(B*0.5)
830  GOSUB9500
840  PRINT"T-VALUE = ";ABS(A)
850  PRINT
860  PRINT
870  PRINT"DEGREE OF FREEDOM = ";B
880  GOSUB9000
890  RETURN
900  CLS
910  PRINTTAB(7);"TUTORIAL SESSION"
920  GOSUB9500
930  PRINTTAB(10);"T - TEST"
940  GOSUB9500
950  PRINT"EG.1 : SAMPLE OF CHILDREN'S"
960  PRINT"I.Q. IS TAKEN AND ASSUME"
970  PRINT"HYPOTHESIS 1 (POPULATION"
980  PRINT"MEAN = GIVEN VALUE)."
990  PRINT
1000 PRINT"ENTER THE SIZE OF SAMPLE,"
1010 PRINT"VALUES OF OBSERVATIONS & THE"
1020 PRINT"ASSUMED POPULATION MEAN."
1030 T=1
1040 GOSUB9500
1050 GOSUB400
1060 PRINT"EG.2 : IF HYPOTHESIS 2 IS"
1070 PRINT"ASSUMED, I.E. MEAN 1 = MEAN"
1080 PRINT"2, STD. DEV. 1 = STD. DEV. 2"
1090 PRINT
1100 PRINT"ENTER 2 SETS OF SAMPLE DATA"
1110 T=2
1120 GOSUB9500
1130 GOSUB400
1140 PRINTTAB(7);"REVIEW QUESTION"
1150 PRINTTAB(7);"==============="
1160 GOSUB9500
1170 PRINT"WHAT VALUE MUST BE KNOWN IN"
1180 PRINT"THE CALCULATION OF T-VALUE."
1190 PRINT
1200 PRINT
1210 PRINT"1. DEGREE OF FREEDOM"
1220 PRINT"2. SAMPLE MEAN"
1230 PRINT"3. ESTIMATED STANDARD ERROR"
1240 PRINT"4. 2 AND 3"
1250 PRINT
1260 PRINT"YOUR CHOICE (1-4)";
1270 INPUTX
1280 GOSUB9500
1290 IFX=4THENPRINT"YES, 4 IS CORRECT."
1300 IFX<>4THENPRINT"INCORRECT, REPEAT TUTORIAL!"
1310 GOSUB9500
1320 GOSUB9000
1330 RUN
3000 CLS
3010 PRINTTAB(7);"TAPE LOADING"
3020 GOSUB9500
3030 GOSUB9500
3040 GOSUB9500
3050 CRUN
4000 CLS
4010 PRINTTAB(8);"END OF PROGRAM"
4020 GOSUB9500
4030 PRINTTAB(5);"PRESS RESET TO STOP"
4040 GOSUB9500
4050 GOSUB9500
4060 GOSUB9500
4070 GOTO4070
9000 PRINT
9010 PRINT
9020 PRINT"HIT -RETURN- TO CONTINUE";
9030 INPUTA$
9040 CLS
9050 RETURN
9500 PRINT
9510 PRINT
9520 PRINT
9530 RETURN
