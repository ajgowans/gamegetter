5    REM : STATISTICS II
7    REM : (PART 1)
10   DIMU(100),V(10),A(10)
13   CLS
14   I=7
15   PRINTTAB(I);"CHI-SQUARE TEST"
16   FORI=1TO12
17   PRINT
18   NEXT
22   FORI=1TO10
23   U(I)=0
24   V(I)=0
25   A(I)=0
26   NEXT
27   FORI=11TO100
28   U(I)=0
29   NEXT
40   CLS
60   PRINT"OPTIONS :"
65   PRINT
70   PRINT
80   PRINT"1. TUTORIAL SESSION"
90   PRINT"2. STATISTICAL PROCEDURE"
100  PRINT"3. SKIP TO NEXT TOPIC"
110  PRINT"4. END PACKAGE"
120  PRINT
130  PRINT
140  PRINT"YOUR CHOICE ";
150  INPUTO
155  IFO=1THENGOSUB700
165  IFO=2THENF=0
166  IFO=2THENGOSUB200
170  IFO=3THEN1500
180  IFO=4THEN3000
190  GOTO40
200  CLS
202  PRINTTAB(7);"CHI-SQUARE TEST"
203  F=0
205  PRINT
210  PRINT"NO. OF ROWS   ";
220  INPUTR
221  IFR<=0THEN210
222  PRINT
223  PRINT"NO. OF COLUMNS";
225  INPUTC
230  IFC<=0THEN223
240  GOSUB400
300  RETURN
400  N=R*C
405  PRINT
406  PRINT
410  PRINT"CONTINGENCY TABLE :"
420  PRINT
465  FORI=1TOR
466  PRINT
467  IFF=1ANDI=1THENPRINT"SLEEPING PILL"
468  IFF=1ANDI=2THENPRINT"PLACEBOS"
470  IFF=0THENPRINT"   ROW : ";I
471  PRINT
473  FORJ=1TOC
475  IFF=1ANDJ=1THENPRINT"SLEPT WELL ";
477  IFF=1ANDJ=2THENPRINT"SLEPT POORLY ";
480  IFF=0THENPRINT"ELEMENT ";J;
483  K=(I-1)*C+J
485  INPUTU(K)
490  NEXTJ
495  NEXTI
500  PRINT
505  L=0
510  M=1
515  FORI=1TOR
520  FORJ=1TOC
525  A(I)=A(I)+U(M)
530  M=M+1
535  NEXTJ
540  L=L+A(I)
545  NEXTI
550  FORI=1TOC
555  FORJ=1TONSTEPC
560  V(I)=V(I)+U(J)
565  NEXTJ
570  NEXTI
575  Z=0
576  CLS
577  PRINT" OBSERVED  EXPECTED   CHI**2"
578  PRINT"   VALUE     VALUE     VALUE"
579  PRINT
580  FORI=1TOC
585  IFF=1ANDI=1THENPRINT"SLEPT WELL"
587  IFF=1ANDI=2THENPRINT"SLEPT POORLY"
590  IFF=0THENPRINT"COLUMN : ";I
595  FORJ=1TOR
600  P=A(J)*V(I)/L
605  X=I+(J-1)*C
609  Y=U(X)-P
610  Y=ABS(Y)
611  Y=Y**2/P
615  Z=Z+Y
617  Y=INT(1000*Y)/1000
618  P=INT(1000*P)/1000
620  PRINTTAB(3);U(X);TAB(14);P;TAB(22);Y
625  NEXTJ
630  NEXTI
635  PRINT
640  PRINT"CHI-SQUARE = ";Z
645  PRINT"DEGREE OF FREEDOM = ";(C-1)*(R-1)
647  GOSUB900
650  RETURN
700  CLS
710  PRINTTAB(7);"CHI-SQUARE TEST"
711  PRINT
713  PRINT"PROVIDES THE BASIS TO COM-"
714  PRINT"PARE OBSERVED FREQUENCY FOR"
715  PRINT"THE POPULATION FROM WHICH A"
716  PRINT"SAMPLE WAS DRAWN WITH"
717  PRINT"THEORETICAL FREQUENCIES DE-"
718  PRINT"RIVED UNDER SPECIFIED PROB-"
719  PRINT"ABILITY DISTRIBUTION OR"
720  PRINT"HYPOTHESIS."
721  GOSUB900
722  PRINT"USUALLY NULL OR ALTERNATIVE"
723  PRINT"HYPOTHESIS ARE ESTABILISHED"
725  PRINT"AND A SIGNIFICANT LEVEL IS"
726  PRINT"SELECTED FOR REJECTION OF"
727  PRINT"THE NULL HYPOTHESIS.  IF THE"
728  PRINT"AGGREGATE DISCREPANCY BE-"
729  PRINT"TWEEN OBSERVED & THEORETICAL"
730  PRINT"FREQUENCY IS TOO GREAT TO"
731  PRINT"ATTRIBUTE TO CHANCE FLUCT-"
732  PRINT"UATIONS AT SELECTED SIGNIFI-"
733  PRINT"CANCE LEVEL, THE NULL HYPO-"
734  PRINT"THESIS IS REJECTED."
747  GOSUB900
750  PRINT"THE TEST STATISTICS CHI**2"
753  PRINT"(X**2) IS SUMMATION OF"
755  PRINT"SQUARED DIFFERENCE BETWEEN"
757  PRINT"OBSERVED & EXPECTED FRE-"
758  PRINT"QUENCIES DIVIDED BY EXPECTED"
759  PRINT"FREQUENCY."
760  PRINT
761  PRINT"X**2 = SUM((FO-FE)**2/FE)"
762  PRINT
763  PRINT"FO = OBSERVED FREQUENCY"
765  PRINT"FE = EXPECTED FREQUENCY"
770  PRINT
775  PRINT"IF EVERY OBSERVED FREQUENCY"
777  PRINT"EQUALS TO CORRESPONDING"
780  PRINT"THEORETICAL FREQUENCY, THE"
785  PRINT"CHI**2 WILL BE 0."
795  GOSUB900
797  GOSUB1000
800  CLS
803  PRINT"EG.1  A GROUP OF PEOPLE WHO"
805  PRINT"COMPLAIND THEY COULD NOT"
810  PRINT"SLEEP WELL.  SOME ARE GIVEN"
813  PRINT"PLACEBOS WHILE OTHERS SLEEP-"
815  PRINT"ING PILLS.  THE EFFECT OF"
820  PRINT"PILLS & PLACEBOS IS SHOWN IN"
825  PRINT"CONTINGENCY TABLE.  FIND THE"
826  PRINT"CHI**2 STATISTICS."
828  PRINT
829  I=5
830  PRINTTAB(I);"CONTINGENCY TABLE"
833  PRINT
835  PRINT"   SLEPT WELL   SLEPT POOR"
837  PRINT"PILLS     1,1   1,2"
840  PRINT"PLACEBOS  2,1   2,2"
845  GOSUB900
847  PRINT
850  PRINT"NO. OF ROWS    = 2"
852  PRINT"NO. OF COLUMNS = 2"
853  PRINT
855  F=1
856  R=2
857  C=2
860  GOSUB400
870  GOSUB950
898  RETURN
900  PRINT
902  FORI=1TO4
905  PRINT
907  NEXT
909  PRINT"----------------------------"
910  PRINT"HIT -RETURN- TO CONTINUE";
912  INPUTA$
917  CLS
920  RETURN
950  PRINT"QUESTION : WHAT DEGREE OF"
953  PRINT"FREEDOM IS FOR A CONTINGENCY"
955  PRINT"TABLE WITH 3 ROWS & 3 COL-"
956  PRINT"UMNS";
959  INPUTX
960  PRINT
961  IFX=4THENPRINT"YES, IT IS CORRECT"
962  IFX<>4THENPRINT"INCORRECT, REPEAT TUTORIAL"
965  GOSUB900
967  RETURN
1000 PRINT"EXPECTED FREQUENCY ROW    I,"
1010 PRINT"EXPECTED FREQUENCY COL.   J,"
1020 PRINT"= (XI*XJ)/X"
1030 PRINT"XI = SUM OF VALUES IN ROW  I"
1040 PRINT"XJ = SUM OF VALUES IN COL. J"
1050 PRINT" X = GRAND TOTAL OF VALUES"
1060 PRINT
1065 PRINT"DEGREE OF FREEDOM IS DEFINED"
1068 PRINT"AS THE NO. OF INDEPENDENT"
1070 PRINT"DEVIATION."
1072 PRINT
1075 PRINT"DEGREE OF FREEDOM :"
1077 PRINT"D.O.F. = (C-1)*(R-1)"
1080 PRINT"C = NO. OF COLUMNS"
1085 PRINT"R = NO. OF ROWS"
1097 GOSUB900
1100 RETURN
1200 CLS
1205 PRINT
1206 PRINT
1210 PRINT"WARNING !!!"
1220 PRINT"THE DIMENSION STATEMENT"
1223 PRINT"LIMITS SIZE OF CONTINGENCY"
1225 GOSUB900
1230 PRINT"DIMENSION ACCORDING TO THE"
1235 PRINT"SCHEME :"
1237 PRINT
1240 PRINT"10   DIMU(R*C),V(C),A(R)"
1245 PRINT"R = NO. OF ROWS"
1250 PRINT"C = NO. OF COLUMNS"
1260 RETURN
1500 CLS
1505 I=8
1510 PRINTTAB(I);"TAPE LOADING"
1520 FORI=1TO10
1530 PRINT
1540 NEXT
1550 CRUN
3000 CLS
3010 PRINTTAB(8);"END OF PROGRAM"
3011 PRINT
3012 PRINT
3013 PRINT
3014 PRINTTAB(5);"PRESS RESET TO STOP"
3015 FORI=1TO10
3020 PRINT
3030 NEXT
3040 GOTO3040
