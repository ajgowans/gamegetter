10   REM : STATISTICS II
20   REM : (PART 4)
40   CLS
50   PRINT"INTRODUCTION TO STATISTICS II"
60   GOSUB9500
70   PRINTTAB(7);"T - DISTRIBUTION"
80   GOSUB9500
85   GOSUB9500
90   PRINT"OPTIONS :"
100  PRINT
110  PRINT"1. TUTORIAL SESSION"
120  PRINT"2. STATISTICAL PROCEDURE"
130  PRINT"3. SKIP TO NEXT TOPIC"
140  PRINT"4. END PACKAGE"
150  GOSUB9500
160  PRINT"YOUR CHOICE (1-4)";
170  INPUTO
180  IFO<=4ANDO=>1THEN200
190  GOTO160
200  CLS
210  IFO=4THEN3000
220  IFO=3THEN4000
230  IFO=2THENF=0
240  IFO=1THENGOSUB900
250  IFO=2THENGOSUB300
260  RUN
300  CLS
310  PRINTTAB(7);"T - DISTRIBUTION"
320  GOSUB9500
330  GOSUB400
340  RETURN
400  PRINT"T-VALUE (MUST <> 0)";
410  INPUTT
420  GOSUB9500
430  PRINT"DEGREE OF FREEDOM ";
440  INPUTD
450  GOSUB9500
460  X=1
470  Y=1
480  T=T**2
490  IFT<1THEN540
500  S=Y
510  R=D
520  Z=T
530  GOTO570
540  S=D
550  R=Y
560  Z=1/T
570  J=2/9/S
580  K=2/9/R
590  L=ABS((1-K)*Z**(1/3)-1+J)
600  P=SQR(K*Z**(2/3)+J)
610  L=L/P
620  IFR<4THEN680
630  X=0.1152+L*(0.00034+L*0.01953)
640  X=1+L*(0.19685+L*X)
650  X=0.5/X**4
660  X=INT(X*10000+0.5)/10000
670  GOTO700
680  L=L*(1+0.08*L**4/R**3)
690  GOTO630
700  IFT=>1THEN720
710  X=1-X
720  PRINT"RIGHT TAIL VALUE = ";X
730  GOSUB9500
740  GOSUB9000
750  RETURN
900  CLS
910  PRINTTAB(7);"TUTORIAL SESSION"
920  GOSUB9500
930  PRINTTAB(7);"T - DISTRIBUTION"
940  GOSUB9500
950  PRINT"STUDENT'S T-DISTRIBUTION :"
955  PRINT
960  PRINT"IS A CONTINUOUS SAMPLING"
970  PRINT"DISTRIBUTION HANDLING WITH"
980  PRINT"SMALL SIZE SAMPLE AND IS A"
990  PRINT"FAMILY OF FUNCTIONS, ONE FOR"
1000 PRINT"EACH VALUE OF V."
1010 PRINT
1020 PRINT"(DEGREE OF FREEDOM)"
1030 GOSUB9000
1040 PRINT"IT CAN BE REPRESENTED AS :"
1050 PRINT
1060 PRINT"F(T)=C*(1+T**2/V)**(-(V+1)/2)"
1065 PRINT
1070 PRINT"T = T-VALUE FROM T-"
1080 PRINT"    DISTRIBUTION TEST AND"
1090 PRINT"    VARIES FROM - INFINITY"
1100 PRINT"    TO + INFINITY"
1110 PRINT
1120 PRINT"C = CONSTANT TO MAKE THE"
1130 PRINT"    AREA UNDER THE CURVE = 1"
1140 PRINT
1150 PRINT"V = DEGREE OF FREEDOM"
1155 GOSUB9500
1160 GOSUB9000
1170 PRINT"IN THE APPLICATION OF T-"
1180 PRINT"DISTRIBUTION ON HYPOTHESIS"
1190 PRINT"TESTING, F(X) FOR KNOWN"
1200 PRINT"VALUE OF V IS PLOTTED AND A"
1210 PRINT"CRITICAL T-VALUE IS SELECTED."
1220 PRINT"THE PROGRAM CALCULATES RIGHT"
1230 PRINT"TAIL VALUE FOR THE T-VALUE &"
1240 PRINT"GIVING PROBABILITY OF HAVING"
1250 PRINT"THAT CRITICAL T-VALUE."
1260 GOSUB9500
1270 PRINT"IF THE PROBABILITY IS TOO"
1280 PRINT"SMALL, THE HYPOTHESIS IS RE-"
1290 PRINT"JECTED ACCORDINGLY.  RIGHT"
1300 PRINT"TAIL VALUE REFER TO AREA"
1310 PRINT"UNDER DISTRIBUTION CURVE BE-"
1320 PRINT"HIND CRITICAL T-VALUE."
1330 GOSUB9000
1340 PRINT"THE RIGHT TAIL VALUE IS"
1350 PRINT"APPROXIMATELY GIVEN BY :"
1360 PRINT
1370 PRINT"RIGHT TAIL (R.T.)"
1380 PRINT"R.T. = 0.5*(1+B)**(-4)+E(X)"
1390 PRINT
1400 PRINT"B = A1X+A2(X**2)+A3(X**3)"
1410 PRINT"    +A4(X**4)"
1420 PRINT"X = CONSTANT DEPENDS ON"
1430 PRINT"    DEGREE OF FREEDOM & T-"
1440 PRINT"    STATISTICS"
1450 PRINT
1460 PRINT"A1 = 0.196854"
1470 PRINT"A2 = 0.115194"
1480 PRINT"A3 = 0.000344"
1490 PRINT"A4 = 0.019527"
1500 PRINT
1510 PRINT"E(X) < 0.25*10**(-4)"
1520 GOSUB9000
1530 PRINT"AS THE SIZE OF SAMPLE/DEGREE"
1540 PRINT"OF FREEDOM INCREASES, T-"
1550 PRINT"DISTRIBUTION WILL APPROACH"
1560 PRINT"TO NORMAL DISTRIBUTION."
1570 GOSUB9500
1580 PRINT"EG.1 : USING THE T-VALUE &"
1590 PRINT"DEGREE OF FREEDOM OBTAINED"
1600 PRINT"FROM THE T-DISTRIBUTION TEST,"
1610 PRINT"CALCULATE THE RIGHT TAIL"
1620 PRINT"VALUE."
1630 GOSUB9500
1640 GOSUB400
1660 PRINTTAB(7);"REVIEW QUESTION"
1670 PRINTTAB(7);"==============="
1680 GOSUB9500
1690 PRINT"IF THE SAMPLE SIZE HAS BEEN"
1700 PRINT"LARGE, BUT WITH UNKNOWN POP-"
1710 PRINT"ULATION STANDARD DEVIATION,"
1720 PRINT"WHAT DISTRIBUTION TEST"
1730 PRINT"SHOULD BE APPLIED TO THE ME-"
1740 PRINT"ASURE OF PRECISION IN EST-"
1750 PRINT"IMATION DURING HYPOTHESIS"
1760 PRINT"TESTING."
1770 PRINT
1780 PRINT"1. NORMAL DISTRIBUTION"
1790 PRINT"2. T-DISTRIBUTION"
1800 PRINT"3. X**2 DISTRIBUTION"
1810 PRINT
1820 PRINT"YOUR CHOICE";
1830 INPUTX
1840 GOSUB9500
1850 IFX=1THENPRINT"YES, 1 IS CORRECT."
1860 IFX<>1THENPRINT"INCORRECT, REPEAT TUTORIAL!"
1870 GOSUB9000
1880 RETURN
3000 CLS
3010 PRINTTAB(8);"END OF PROGRAM"
3013 GOSUB9500
3015 PRINTTAB(5);"PRESS RESET TO STOP"
3020 GOSUB9500
3030 GOSUB9500
3040 GOSUB9500
3100 GOTO3100
4000 CLS
4010 PRINTTAB(8);"TAPE LOADING"
4020 GOSUB9500
4025 GOSUB9500
4030 GOSUB9500
4040 GOSUB9500
4100 CRUN
9000 PRINT
9010 PRINT
9020 PRINT"HIT -RETURN- TO CONTINUE";
9030 INPUTA$
9035 CLS
9040 RETURN
9500 PRINT
9505 PRINT
9510 PRINT
9520 RETURN
