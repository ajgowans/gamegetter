10   REM : STATISTICS II
20   REM : (PART 7)
40   CLS
50   PRINT"INTRODUCTION TO STATISTICS II"
60   GOSUB9500
70   PRINTTAB(5);"LINEAR REGRESSION"
80   GOSUB9500
85   GOSUB9500
90   PRINT"OPTIONS :"
100  PRINT
110  PRINT"1. TUTORIAL SESSION"
120  PRINT"2. STATISTICAL PROCEDURE"
140  PRINT"3. END PACKAGE"
150  GOSUB9500
160  PRINT"YOUR CHOICE (1-3)";
170  INPUTO
180  IFO<=3ANDO=>1THEN200
190  GOTO160
200  CLS
220  IFO=3THEN3000
230  IFO=2THENF=0
240  IFO=1THENGOSUB900
250  IFO=2THENGOSUB300
260  RUN
300  CLS
310  PRINTTAB(5);"LINEAR REGRESSION"
320  GOSUB9500
330  F=0
340  GOSUB400
350  RETURN
400  IFF=0THEN430
410  PRINT"NO. OF OBSERVATIONS MAKE"
420  GOTO440
430  PRINT"NO. OF KNOWN POINTS"
440  PRINT"(MUST => 3)";
450  INPUTN
460  GOSUB9500
470  J=0
480  K=0
490  M=0
500  L=0
510  S=0
520  FORI=1TON
530  IFF=0THENPRINT"X,Y OF POINT ";I;
540  IFF=1THENPRINT"X,Y OF OBSERVATION ";I;
550  INPUTX,Y
560  J=J+X
570  K=K+Y
580  L=L+X**2
590  M=M+Y**2
600  S=S+X*Y
610  NEXT
620  B=(N*S-K*J)/(N*L-J**2)
630  A=(K-B*J)/N
640  GOSUB9500
650  A=10000*A
660  A=INT(A)
670  A=A/10000
680  B=10000*B
690  B=INT(B)
700  B=B/10000
710  IFF=1THENPRINT"DERIVED EQUATION :"
720  PRINT
730  PRINT"F(X) = ";A;
740  IFB>0THENPRINT"+";
750  PRINTB;"*X"
760  J=B*(S-J*K/N)
770  M=M-K**2/N
780  K=M-J
790  GOSUB9500
800  S=J/M
810  PRINT"COEFFICIENT OF DETERMINATION"
820  PRINT"(R**2) = ";S
830  GOSUB9500
840  PRINT"COEFFICIENT OF CORRELATION"
850  PRINT"   (R) = ";SQR(S)
860  GOSUB9500
870  PRINT"STANDARD ERROR OF ESTIMATION"
875  K=ABS(K)
880  IFK=0THENPRINT"(S.E.) = 0"
881  IFK=0THEN884
882  PRINT"(S.E.) = ";SQR(K/(N-2))
884  GOSUB9500
886  IFF=1THENGOSUB8000
888  PRINT"INTERPOLATION : (0 TO END)"
889  PRINT
890  PRINT"X = ";
891  INPUTX
892  GOSUB9500
893  IFX=0THEN898
895  PRINT"Y = ";A+B*X
896  GOSUB9500
897  GOTO890
898  GOSUB9000
899  RETURN
900  CLS
910  PRINTTAB(7);"TUTORIAL SESSION"
920  GOSUB9500
930  PRINTTAB(6);"LINEAR REGRESSION"
940  GOSUB9500
950  PRINT"LINEAR REGRESSION : PROVIDES"
960  PRINT"THE BASIS FOR PREDICTING"
970  PRINT"VALUE OF A VARIABLE FROM THE"
980  PRINT"VALUES OF 1/MORE VARIABLES"
990  PRINT"WHICH FIT INTO A LINEAR RE-"
1000 PRINT"LATIONSHIP OF Y=A+BX WHERE"
1010 PRINT"A & B ARE CONSTANTS."
1020 GOSUB9500
1030 GOSUB9000
1040 PRINT"BY THE METHOD OF LEAST"
1050 PRINT"SQUARE, THE PROGRAM DERIVES"
1060 PRINT"A REGRESSION LINE WHICH DES-"
1070 PRINT"CRIBES THE AVERAGE RELATION-"
1080 PRINT"SHIP BETWEEN X & Y VARIABLES"
1090 PRINT"HENCE, PREDICTING THE MEAN"
1100 PRINT"VALUE OF Y FOR A GIVEN VALUE"
1110 PRINT"OF X."
1120 GOSUB9500
1125 GOSUB9000
1130 PRINT"THE SECOND GOAL OF PROGRAM"
1140 PRINT"IS TO OBTAIN MEASURES OF THE"
1150 PRINT"ERROR INVOLVED IN USING THE"
1160 PRINT"REGRESSION LINE AS A BASIS"
1170 PRINT"OF ESTIMATION.  FOR THIS"
1180 PRINT"PURPOSE, THE STANDARD ERROR"
1190 PRINT"OF ESTIMATE IS CALCULATED TO"
1200 PRINT"MEASURE THE SPREAD OF OB-"
1210 PRINT"SERVED VALUES OF Y AROUND"
1220 PRINT"THE CORRESPONDING VALUES"
1230 PRINT"ESTIMATED FROM THE FITTED"
1240 PRINT"REGRESSION LINE."
1245 GOSUB9500
1250 GOSUB9000
1260 PRINT"THE THIRD OBJECTIVE OF THIS"
1270 PRINT"PROGRAM IS TO OBTAIN COE-"
1280 PRINT"FFICIENT OF CORRELATION"
1290 PRINT"WHICH MEASURES DEGREE OF"
1300 PRINT"ASSOCIATION BETWEEN THE TWO"
1310 PRINT"VARIABLES."
1320 GOSUB9500
1330 GOSUB9000
1340 PRINT"METHOD OF LEAST SQUARES :"
1350 PRINT"THIS METHOD IMPOSES THE RE-"
1360 PRINT"QUIREMENT THAT THE SUM OF"
1370 PRINT"THE OBSERVED DEPENDENT VARI-"
1380 PRINT"ABLE (Y) FROM THE CORR-"
1390 PRINT"RESPONDING COMPUTED VALUE"
1400 PRINT"(YC) ON THE REGRESSION LINE"
1410 PRINT"MUST BE A MINIMUM."
1420 PRINT
1430 PRINT"I.E.  SUM(Y-YC)**2 = 0"
1440 GOSUB9000
1450 PRINT"HENCE 2 NORMAL EQUATIONS"
1460 PRINT"MUST BE SOLVED TO DERIVE A &"
1470 PRINT"B IN THE REGRESSION EQUATION"
1480 PRINT"Y=A+BX."
1482 PRINT
1485 PRINT"  SUM(Y) = N*A+BSUM(X)"
1490 PRINT
1500 PRINT"SUM(X*Y) = ASUM(X)+BSUM(X**2)"
1510 PRINT
1520 PRINT"SOLVING A & B :"
1530 PRINT
1540 PRINT"A = YM-B*XM"
1550 PRINT"B = C/D"
1560 PRINT
1570 PRINT" C = SUM(X*Y)-X*XM*YM"
1580 PRINT" D = SUM(X**2)-N*XM**2"
1590 PRINT" X = OBSERVED X VALUE"
1600 PRINT" Y = OBSERVED Y VALUE"
1610 PRINT"XM = ARITHEMATIC MEAN OF X"
1620 PRINT"YM = ARITHEMATIC MEAN OF Y"
1630 GOSUB9000
1640 PRINT"STANDARD ERROR OF ESTIMATE"
1650 PRINT"IS THE MEASURE OF DISPERSION."
1660 PRINT"S = SQR(K/(N-2))"
1670 PRINT"K = SUM(Y**2)-A*SUM(Y)"
1680 PRINT"    -B*SUM(X*Y)"
1690 GOSUB9500
1700 PRINT"COEFFICIENT OF CORRELATION :"
1710 PRINT
1720 PRINT"R = SQR(C/D)"
1730 PRINT"D = SUM(Y**2)-N*(Y**2)"
1740 PRINT"C = A*SUM(Y)+B*SUM(X*Y)"
1750 PRINT"    -N*SUM(Y**2)"
1760 GOSUB9000
1770 PRINT"EG.1 : TO DERIVE A EQUATION"
1780 PRINT"WHICH FITS THE RELATIONSHIP"
1790 PRINT"BETWEEN THE HEIGHT & WEIGHT"
1800 PRINT"OF COLLEGE STUDENT.  FIRST"
1810 PRINT"ENTER NO. OF OBSERVATIONS"
1820 PRINT"MADE AND THE WEIGHT & HEIGHT"
1830 PRINT"PAIR RECORDED."
1840 GOSUB9500
1850 F=1
1860 GOSUB400
1870 PRINTTAB(7);"REVIEW QUESTION"
1880 PRINTTAB(7);"==============="
1890 GOSUB9500
1900 PRINT"IN DERIVATION OF REGRESSION"
1910 PRINT"LINE BY THE METHOD OF LEAST"
1920 PRINT"SQUARE, THE SUM OF DEVIATION"
1930 PRINT"OF THE OBSERVED VALUES OF Y"
1940 PRINT"FROM THE CORRESPONDING COMP-"
1950 PRINT"UTED Y ON REGRESSION LINE"
1960 PRINT"SHOULD BE MADE TO EQUAL TO M"
1970 PRINT
1980 PRINT
1990 PRINT"VALUE OF M = ";
2000 INPUTX
2010 GOSUB9500
2020 IFX=0THENPRINT"YES, 0 IS CORRECT."
2030 IFX<>0THENPRINT"INCORRECT, REPEAT TUTORIAL!"
2040 GOSUB9500
2050 GOSUB9000
2060 RUN
3000 CLS
3010 PRINTTAB(8);"END OF PROGRAM"
3013 GOSUB9500
3015 PRINTTAB(5);"PRESS RESET TO STOP"
3020 GOSUB9500
3030 GOSUB9500
3040 GOSUB9500
3100 GOTO3100
8000 GOSUB9000
8010 PRINT"AFTER GETTING THE REGRESSION"
8020 PRINT"LINE, YOU CAN NOW ENTER X"
8030 PRINT"VALUE AND OBTAIN Y VALUE BY"
8040 PRINT"INTERPOLATION METHOD."
8050 GOSUB9500
8060 RETURN
9000 PRINT
9010 PRINT
9020 PRINT"HIT -RETURN- TO CONTINUE";
9030 INPUTA$
9040 CLS
9050 RETURN
9500 PRINT
9510 PRINT
9520 PRINT
9530 RETURN
