5    DIM A(4),B(4),C(4)
10   CLS
20   PRINT TAB(8);"CODE BREAKER"
21   FOR I=1 TO 5
22   PRINT
23   NEXT
24   LET Q=0
25   LET W=0
26   P=0
30   LET D=1
35   PRINT"     (1) BASIC LEVEL"
40   PRINT"        (FOR SINGLE NOS.)"
41   PRINT
42   PRINT"     (2) ADVANCE LEVEL"
43   PRINT"        (FOR DOUBLE NOS.)"
44   FOR I=1 TO 5
45   PRINT
46   NEXT
47   PRINT"            SELECT (1/2)";
50   INPUT S$
51   CLS
52   PRINT"      GENERATING A NUMBER"
53   PRINT
54   PRINT"         PLEASE WAIT !"
56   FOR I=1 TO 10
57   PRINT
58   NEXT
60   IF S$="1" THEN GOSUB 1000
64   IF S$="2" THEN GOSUB 2000
67   IF S$="1" THEN 71
68   IF S$="2" THEN 71
70   RUN
71   FOR I=1 TO 8
72   FOR I=1 TO 5
73   PRINT
74   NEXT
75   PRINT"            ENTER"
76   PRINT
77   PRINT"     ONE DIGIT AT A TIME"
78   PRINT
79   PRINT"            (1-6)"
80   FOR I=1 TO 4
81   PRINT
82   NEXT
83   IF D>10 THEN 85
84   GOTO 100
85   PRINT"HARD LUCK !"
86   PRINT"NO. SHOULD BE:";
87   FOR I=1 TO 4
88   PRINT A(I);" ";
89   NEXT
90   PRINT
95   PRINT"TRY AGAIN ";
96   INPUT Y$
97   IF LEFT$(Y$,1)="Y" THEN RUN
98   END
100  PRINT"CHANCE ";D;":"
110  FOR A=1 TO 4
114  LET F=0
115  PRINT
116  PRINT TAB(6);"DIGIT ";A;" ";
120  INPUT N
130  GOSUB 500
135  IF F=1 THEN 114
140  LET C(A)=N
160  NEXT A
170  D=D+1
180  FOR A=1 TO 4
190  IF C(A)<>B(A) THEN 230
200  LET Q=Q+1
210  LET C(A)=888
220  LET B(A)=999
230  NEXT A
240  FOR A=1 TO 4
250  FOR B=1 TO 4
260  IF C(A)<>B(B) THEN 300
270  LET W=W+1
280  LET B(B)=999
290  LET C(A)=888
300  NEXT B
310  NEXT A
315  PRINT
320  PRINT"TOTAL NO. OF BLACK:";Q
321  PRINT"TOTAL NO. OF WHITE:";W
330  IF Q<>4 THEN 340
331  PRINT
332  PRINT"GOOD !";
333  SOUND 4;5,6;7,7;7
335  GOTO 95
340  PRINT 
360  LET W=0
370  LET Q=0
380  FOR A=1 TO 4
390  LET B(A)=A(A)
400  NEXT A
405  PRINT
406  PRINT
407  PRINT"WOULD YOU LIKE TO CONTINUE"
408  PRINT"(Y/N)";
409  INPUT Z$
410  PRINT
411  PRINT
412  IF Z$="N" THEN 85
415  GOTO 83
500  IF N>=1 THEN 510
501  PRINT "NO. SHOULD BE (1-6)"
503  LET F=1
504  GOTO 520
510  IF N=<6 THEN GOTO 520
511  PRINT "NO. SHOULD BE (1-6)"
513  LET F=1
520  RETURN
530  END
1000 FOR I=1 TO 4
1010 LET A(I)=RND(6)
1020 LET B(I)=A(I)
1030 NEXT I
1031 LET M=0
1040 IF A(1)=A(3) THEN M=1
1050 IF A(1)=A(4) THEN M=1
1060 IF A(2)=A(4) THEN M=1
1070 FOR I=1 TO 3
1080 IF A(I)=A(I+1) THEN M=1
1090 NEXT I
1100 IF M=1 THEN 1000
1102 LET P=1
1200 RETURN
2000 FOR I=1 TO 4
2010 LET A(I)=RND(6)
2020 LET B(I)=A(I)
2030 NEXT I
2031 CLS
2032 LET P=1
2033 PRINT"             NOTE"
2034 PRINT"             ----"
2035 PRINT
2036 PRINT"     NO. MAY BE REPEATED"
2040 RETURN
