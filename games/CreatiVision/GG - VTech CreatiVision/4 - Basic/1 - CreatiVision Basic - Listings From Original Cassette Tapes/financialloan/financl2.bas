10   REM FINANCIAL LOAN
15   CLS
20   P$="       "
30   GOSUB 9040
50   PRINT
60   P$="   "
70   GOSUB 9040
90   PRINT
100  PRINT
110  PRINT"    * FINANCIAL LOAN *"
120  PRINT
130  PRINT"           (2)"
140  PRINT
150  PRINT
160  PRINT"  1. REGULAR PAYMENT"
170  PRINT
180  PRINT
190  PRINT"  2. TERM"
200  PRINT
210  PRINT
220  PRINT"  E. EXIT"
230  PRINT
240  PRINT
250  PRINT
260  PRINT
270  PRINT
280  PRINT"    SELECT ---> ";
290  INPUT A$
300  IF A$<>"E"THENIFA$<>"1"THENIFA$<>"2"THEN470
310  IF A$="E" THEN 430
320  IF A$="1" THEN GOSUB 900
330  IF A$="2" THEN GOSUB 1300
380  GOTO 10
400  IF A$>"6" THEN 470
405  IF A$="" THEN 470
410  GOTO 340
430  CLS
440  END
470  E$="ERROR: INVALID ENTRY !"
480  GOSUB 8000
490  GOTO 10
900  REM REGULAR PAYMENT
905  GOSUB 4000
910  CLS
920  PRINT"*************************"
930  PRINT
940  PRINT"REGULAR PAYMENT ON A LOAN"
950  PRINT
960  PRINT"*************************"
970  PRINT
980  PRINT
990  PRINT"TERM IN YEARS, MONTHS"
995  PRINT
1000 INPUT Z,M
1010 Y=(12*Z+M)/12
1020 PRINT
1030 PRINT"PRINCIPAL"
1040 PRINT
1050 INPUT P
1060 PRINT
1070 PRINT"ANNUAL INTEREST RATE (%)"
1080 PRINT
1090 INPUT I
1100 PRINT
1110 PRINT"NO. OF PAYMENTS PER YEAR"
1120 PRINT
1130 INPUT N
1140 REM CALCULATE AMOUNT OF REGULAR PAYMENT BY FORMULA
1150 R=((I/100)*P/N)/(1-1/((I/100)/N+1)**(N*Y))
1160 PRINT
1170 PRINT"REGULAR PAYMENT = $";R
1180 GOSUB 9500
1190 IF X$="Y" THEN 900
1200 RETURN
1300 REM TERM OF A LOAN
1305 GOSUB 5000
1310 CLS
1320 PRINT"**************"
1330 PRINT
1340 PRINT"TERM OF A LOAN"
1350 PRINT
1360 PRINT"**************"
1370 PRINT
1380 PRINT
1390 PRINT"REGULAR PAYMENT"
1400 PRINT
1410 INPUT R
1420 PRINT
1430 PRINT"PRINCIPAL"
1440 PRINT
1450 INPUT P
1460 PRINT
1470 PRINT"ANNUAL INTEREST RATE (%)"
1480 PRINT
1490 INPUT I
1500 I=I/100
1510 PRINT
1520 PRINT"NO. OF PAYMENTS PER YEAR"
1530 PRINT
1540 INPUT N
1550 Y=(LOG(1-(P*I)/(N*R))/(LOG(1+I/N)*N))
1555 Y=0-Y
1560 REM CALCULATE YEARS AND MONTHS FROM YEARS
1570 V=INT(Y*12+0.5)
1580 Z=V/12
1590 M=V-Z*12
1600 PRINT
1610 PRINT"TERM = ";Z;" YEARS ";
1620 IF M THEN PRINT"&";M;" MONTH";
1630 IF M>1 THEN PRINT"S" 
1640 PRINT
1650 GOSUB 9500
1660 IF X$="Y" THEN 1300
1670 RETURN
1700 REM ANNUAL INTEREST RATE
1710 CLS
1720 PRINT"********************"
1725 PRINT
1730 PRINT"ANNUAL INTEREST RATE"
1740 PRINT
1750 PRINT"********************"
1760 PRINT
1770 PRINT
1780 PRINT"REGULAR PAYMENT"
1790 PRINT
1800 INPUT R
1810 PRINT
1820 PRINT"TERM IN YEARS, MONTHS"
1830 PRINT
1840 INPUT Z,M
1850 Y=(12*Z+M)/12
1860 PRINT
1870 PRINT"PRINCIPAL"
1880 PRINT
1890 INPUT P
1900 PRINT
1910 PRINT"NO. OF PAYMENTS PER YEAR"
1920 PRINT
1930 INPUT N
1940 REM GUESS AN INTEREST RATE (10%) TO INITIATE TESTING
1950 I=10
1960 REM J=LAST GUESS OR ESTIMATE (START WITH 0)
1970 J=0
1980 REM COMPUTE REGULAR PAYMENT USING GUESSED INT. RATE
1990 S=((I/100)*P/N)/(1-1/((I/100)/N+1)**(N*Y))
2000 S=INT(S*100+0.5)/100
2010 K=ABS(I-12)/2
2020 J=I
2025 D=ABS(R-S)
2030 IF D<=2 THEN 2100
2040 IF S>R THEN 2070
2050 I=I+K
2060 GOTO 1990
2070 I=I-K
2080 GOTO 1990
2100 PRINT
2110 PRINT"ANNUAL INTEREST RATE = ";I;"%"
2120 GOSUB 9500
2130 IF X$="Y" THEN 1700
2140 RETURN
2300 REM LAST PAYMENT
2310 CLS
2320 PRINT"**********************"
2330 PRINT
2340 PRINT"LAST PAYMENT ON A LOAN"
2350 PRINT
2360 PRINT"**********************"
2370 PRINT
2380 PRINT
2390 PRINT"REGULAR PAYMENT"
2400 PRINT
2410 INPUT R
2420 PRINT
2430 PRINT"PRINCIPAL"
2440 PRINT
2450 INPUT P
2460 PRINT
2470 PRINT"TERM IN YEARS, MONTHS"
2480 PRINT
2490 INPUT Z,M
2500 Y=(12*Z+M)/12
2510 PRINT
2520 PRINT"ANNUAL INTEREST RATE (%)"
2530 PRINT
2540 INPUT I
2550 I=I/100
2560 PRINT
2570 PRINT"NO. OF PAYMENTS PER YEAR"
2580 PRINT
2590 INPUT N
2600 B=P
2610 FOR J=1 TO N*Y
2620 H=INT((B*I/N)*100+0.5)/100
2630 A=R-H
2640 B=B-A
2650 NEXT J
2660 PRINT
2670 PRINT"LAST PAYMENT = $";R+B
2680 GOSUB 9500
2690 IF X$="Y" THEN 2300
2700 RETURN
2900 REM REMAINING BALANCE
2905 CLS
2910 PRINT"*****************"
2920 PRINT
2930 PRINT"REMAINING BALANCE"
2940 PRINT
2950 PRINT"*****************"
2960 PRINT
2970 PRINT
2980 PRINT"REGULAR PAYMENT"
2990 PRINT
3000 INPUT R
3010 PRINT
3020 PRINT"PRINCIPAL"
3030 PRINT
3040 INPUT P
3050 PRINT
3060 PRINT"NO. OF PAYMENTS PER YEAR"
3070 PRINT
3080 INPUT N
3090 PRINT
3100 PRINT"ANNUAL INTEREST RATE (%)"
3110 PRINT
3120 INPUT I
3130 I=I/100
3140 PRINT
3150 PRINT"LAST PAYMENT MADE "
3155 PRINT
3160 PRINT"(PAYMENT NO., YEAR)"
3170 PRINT
3180 INPUT M,Y
3190 B=P
3200 FOR J=1 TO N*(Y-1)+M
3210 H=INT((B*I/N)*100+0.5)/100
3220 A=R-H
3230 B=B-A
3240 NEXT J
3250 PRINT
3260 PRINT"REMAINING BALANCE = $";B
3270 GOSUB 9500
3280 IF X$="Y" THEN 2900
3290 RETURN
4000 CLS
4020 PRINT
4030 PRINT"      INTRODUCTION"
4040 PRINT
4050 PRINT"THIS PROGRAM CALCULATES THE"
4055 PRINT
4060 PRINT"REGULAR PAYMENTS TO REPAY"
4070 PRINT
4080 PRINT"A LOAN OVER A SPECIFIED TIME"
4090 PRINT
4100 PRINT"PERIOD. YOU MUST PROVIDE THE"
4110 PRINT
4120 PRINT"PRINCIPAL, THE INTEREST RATE"
4130 PRINT
4140 PRINT"CHARGED, THE NO. OF PAYMENTS"
4150 PRINT
4160 PRINT"PER YEAR, AND THE NO. OF"
4170 PRINT
4180 PRINT"YEARS TO PAY. IT ASSUMES ALL"
4190 PRINT
4200 PRINT"INSTALLMENT PAYMENTS WILL"
4210 PRINT
4220 PRINT"BE EQUAL."
4230 GOSUB 9000
4240 RETURN
5000 CLS
5010 PRINT
5020 PRINT"       INTRODUCTION"
5030 PRINT
5040 PRINT"THIS PROGRAM CALCULATES THE"
5050 PRINT
5060 PRINT"PERIOD OF TIME NEEDED TO"
5065 PRINT
5070 PRINT"REPAY A LOAN. YOU MUST"
5080 PRINT
5090 PRINT"SPECIFY THE AMOUNT OF THE"
5100 PRINT
5110 PRINT"LOAN, THE AMOUNT OF THE"
5120 PRINT
5130 PRINT"PAYMENTS, THE NUMBER OF"
5140 PRINT
5150 PRINT"PAYMENTS PER YEAR, AND THE"
5160 PRINT
5170 PRINT"ANNUAL INTEREST RATE ON THE"
5180 PRINT
5190 PRINT"LOAN. ALL PAYMENTS ARE"
5200 PRINT
5210 PRINT"ASSUMED TO BE EQUAL."
5220 GOSUB 9000
5230 RETURN
8000 REM ERROR MESSAGE
8010 PRINT E$
8020 GOSUB 9000
8030 RETURN
9000 REM HIT RETURN
9010 PRINT
9020 PRINT"HIT -RETURN- TO CONTINUE ";
9030 INPUT A$
9035 RETURN
9040 REM PRINT BLANK CHAR.
9050 PRINT P$;
9060 RETURN
9500 REM END OF THIS PROGRAM ?
9510 PRINT
9520 PRINT"TRY AGAIN (Y/N) ";
9530 INPUT X$
9540 IF X$="Y" THEN 9570
9550 IF X$="N" THEN 9570
9560 E$="ERROR: INVALID ENTRY !"
9562 GOSUB 8000
9565 GOTO 9510
9570 RETURN
