10  REM :MATRIX
15  DIM A(10,10),B(10,10),C(10,10)
18  PRINT 
20  HOME 
30  PRINT  TAB( 7);"FUNCTION"
35  PRINT  TAB( 7);"========"
40  PRINT 
50  PRINT 
60  PRINT "  1. MATRIX ADDITION"
70  PRINT "  2. MATRIX SUBTRACTION"
80  PRINT "  3. SCALAR MULTIPLICATION"
90  PRINT "  4. MATRIX MULTIPLICATION"
100  PRINT 
110  PRINT 
120  PRINT "     FUNCTION (1 TO 4) ";
130  INPUT A
140  IF A = 1 THEN 180
150  IF A = 2 THEN 180
160  IF A = 3 THEN 180
170  IF A = 4 THEN 180
175  GOTO 120
180  PRINT 
185  PRINT 
200  PRINT "     DIMENSION OF MATRIX"
205  PRINT 
210  PRINT "     (MAX. IS 10) ";
220  INPUT B
230  IF B > 10 THEN 210
240  IF B <  = 0 THEN 200
250  IF A = 1 THEN 300
255  IF A = 2 THEN 800
260  IF A = 4 THEN 1200
270  IF A = 3 THEN 1800
300  HOME 
310  PRINT "IN MATRIX ADDITION, YOU NEED"
320  PRINT "TO INPUT VALUE OF ELEMENTS"
330  PRINT "OF 2 MATRICES (A & B)."
340  PRINT : PRINT 
350  PRINT "DIMENSION OF MATRICES"
360  PRINT "A & B MUST BE THE SAME."
390  GOSUB 5000
400  FOR M = 1 TO D
410  FOR N = 1 TO C
420 C(N,M) = A(N,M) + B(N,M)
430  NEXT 
440  NEXT 
450  HOME 
460  PRINT "RESULT : C = A + B"
470  PRINT : PRINT 
480  FOR M = 1 TO D
490  FOR N = 1 TO C
495  PRINT "  ";
500  PRINT "C(";N;",";M;") = ";C(N,M)
510  NEXT 
520  NEXT 
530  GOSUB 7000
540  RUN 
800  HOME 
810  PRINT "IN MATRIX SUBTRACTION, YOU"
820  PRINT "NEED TO INPUT THE VALUE OF"
830  PRINT "ELEMENTS OF 2 MATRICES"
840  PRINT "(A & B)."
860  GOSUB 5000
870  FOR M = 1 TO D
880  FOR N = 1 TO C
890 C(N,M) = A(N,M) - B(N,M)
900  NEXT 
910  NEXT 
920  HOME 
930  PRINT "RESULT : C = A - B"
940  PRINT 
950  PRINT 
960  FOR M = 1 TO D
970  FOR N = 1 TO C
980  PRINT "  ";"C(";N;",";M;") = ";C(N,M)
990  NEXT 
1000  NEXT 
1010  GOSUB 7000
1030  RUN 
1200  HOME 
1210  PRINT "IN MATRIX MULTIPLICATION,"
1220  PRINT "YOU NEED TO INPUT THE VALUE"
1230  PRINT "OF ELEMENTS OF 2 MATRICES "
1240  PRINT "(A & B)."
1250  PRINT : PRINT 
1260  PRINT "IF C = (A)(B);"
1270  PRINT "NO. OF COLUMNS OF A MUST BE "
1280  PRINT "THE SAME AS NO. OF ROWS IN"
1290  PRINT "B."
1320  GOSUB 5000
1330  FOR X = 1 TO C
1340  FOR Y = 1 TO E
1350  FOR Z = 1 TO D
1360 C(X,Y) = C(X,Y) + A(X,Z) * B(Z,Y)
1370  NEXT 
1380  NEXT 
1390  NEXT 
1400  HOME 
1410  PRINT "RESULT : C = (A)(B)"
1420  PRINT 
1430  PRINT 
1440  FOR M = 1 TO E
1450  FOR N = 1 TO C
1460  PRINT "  ";"C(";N;",";M;") = ";C(N,M)
1470  NEXT 
1480  NEXT 
1490  GOSUB 7000
1500  RUN 
1800  HOME 
1810  PRINT "IN SCALAR MULTIPLICATION,"
1820  PRINT "YOU NEED TO INPUT THE VALUE"
1830  PRINT "OF ELEMENTS OF THE MATRIX &"
1840  PRINT "THE VALUE OF SCALAR."
1850  PRINT 
1860  PRINT 
1870  PRINT "ENTER VALUE OF SCALAR ";
1875  INPUT S
1880  PRINT 
1890  PRINT "VALUE OF SCALAR = ";S
1900  PRINT 
1920  PRINT "ENTRY CORRECT (Y=YES) ";
1930  INPUT A$
1940  IF A$ <  > "Y" THEN 1850
1960  GOSUB 5000
1970  FOR M = 1 TO D
1980  FOR N = 1 TO C
1990 C(N,M) = A(N,M) * S
2000  NEXT 
2010  NEXT 
2020  HOME 
2030  PRINT "RESULT : C = S(A)"
2040  PRINT 
2050  PRINT 
2060  FOR M = 1 TO D
2070  FOR N = 1 TO C
2080  PRINT "  ";"C(";N;",";M;") = ";C(N,M)
2090  NEXT 
2100  NEXT 
2110  GOSUB 7000
2120  RUN 
5000  PRINT : PRINT : PRINT "DIMENSION OF MATRIX A"
5010  PRINT 
5020  PRINT "NO. OF ROWS    ";
5030  INPUT C
5040  IF C <  = 0 THEN 5020
5050  IF C > B THEN  PRINT 
5060  IF C > B THEN  PRINT "MAX. DIMENSION : ";B
5070  PRINT 
5080  IF C > B THEN 5020
5090  PRINT "NO. OF COLUMNS ";
5100  INPUT D
5110  IF D <  = 0 THEN 5090
5120  IF D > B THEN  PRINT 
5130  IF D > B THEN  PRINT "MAX. DIMENSION : ";B
5140  PRINT 
5150  IF D > B THEN 5090
5180  PRINT 
5290  PRINT "ELEMENTS OF MATRIX A"
5300  PRINT 
5320  FOR M = 1 TO D
5330  FOR N = 1 TO C
5340  PRINT "A(";N;",";M;") = ";
5350  INPUT A(N,M)
5360  NEXT 
5370  NEXT 
5380  HOME 
5390  PRINT "ELEMENTS OF MATRIX A"
5400  PRINT 
5410  FOR M = 1 TO D
5420  FOR N = 1 TO C
5430  PRINT "A(";N;",";M;") = ";A(N,M)
5440  NEXT 
5450  NEXT 
5460  PRINT 
5470  PRINT "ENTRY CORRECT (Y=YES) ";
5480  INPUT A$
5490  IF A$ <  > "Y" THEN 5000
5495  IF A = 3 THEN  RETURN 
5500  HOME 
5505  IF A = 4 THEN 5530
5510  PRINT "DIMENSION OF MATRIX B MUST"
5520  PRINT "EQUAL TO MATRIX A."
5530  PRINT 
5540  PRINT 
5550  PRINT "DIMENSION OF MATRIX B"
5560  PRINT 
5565  IF A = 4 THEN 5575
5570  PRINT "NO. OF ROWS    = ";C: GOTO 5580
5575  PRINT "NO. OF ROWS    = ";D
5580  PRINT 
5582  IF A = 4 THEN  PRINT "NO. OF COLUMNS = ";
5583  IF A = 4 THEN  INPUT E
5584  IF A = 4 THEN  PRINT 
5585  IF A = 4 THEN  PRINT "NO. OF COLUMNS = ";E
5586  IF A = 4 THEN 5600
5590  PRINT "NO. OF COLUMNS = ";D
5600  PRINT : PRINT 
5610  PRINT "ELEMENTS OF MATRIX B"
5620  PRINT 
5630  IF A <  > 4 THEN  FOR M = 1 TO D
5635  IF A = 4 THEN  FOR M = 1 TO E
5640  IF A = 4 THEN  FOR N = 1 TO D
5642  IF A <  > 4 THEN  FOR N = 1 TO C
5650  PRINT "B(";N;",";M;") = ";
5660  INPUT B(N,M)
5670  NEXT 
5680  NEXT 
5690  HOME 
5700  PRINT "ELEMENTS OF MATRIX B"
5710  PRINT 
5720  IF A <  > 4 THEN  FOR M = 1 TO D
5725  IF A = 4 THEN  FOR M = 1 TO E
5730  IF A <  > 4 THEN  FOR N = 1 TO C
5732  IF A = 4 THEN  FOR N = 1 TO D
5740  PRINT "B(";N;",";M;") = ";B(N,M)
5750  NEXT 
5760  NEXT 
5780  PRINT 
5800  PRINT "ENTRY CORRECT (Y=YES) ";
5810  INPUT A$
5820  IF A$ <  > "Y" THEN 5840
5830  RETURN 
5840  IF A = 4 THEN  GOTO 5505
5850  IF A <  > 4 THEN 5600
7000  PRINT 
7010  PRINT 
7020  PRINT 
7030  PRINT "HIT -RETURN- TO CONTINUE ";
7040  INPUT A$
7050  RETURN 
8000  HOME 
8010  PRINT "DIMENSION OF MATRIX B"
8020  PRINT 
8030  PRINT "NO. OF ROWS   = ";D
8040  PRINT 
8050  PRINT "INPUT NO. OF COLUMNS OF B";
8060  INPUT E
8070  PRINT 
8080  PRINT "NO.OF COLUMNS = ";E
8090  PRINT 
8100  PRINT "ENTRY CORRECT (Y=YES) ";
8110  INPUT A$
8120  IF A$ <  > "Y" THEN 8000
8140  RETURN 
