10   REM *TEST MARKER*
20   REM (WRITTEN BY WAYNE G. RICHMOND)
30   REM (NOVEMBER 1982)
40   REM
50   CLS
80   GOSUB 4000
100  REM *INPUT ANSWERS*
110  GOSUB 9000
120  S=0
130  PRINT"TYPE IN THE CORRECT ANSWERS"
135  PRINT
140  PRINT"(NUMBERS ONLY)"
150  PRINT
160  FOR N = 1 TO Q
170  PRINT"QUESTION #";N;
180  INPUT A(N)
190  NEXT
200  GOSUB 9000
205  T = 0
207  PRINT"* ANSWER CHECK *"
208  PRINT
210  FOR N = 1 TO Q
215  T = T + 1
220  PRINT "(";N;") ";A(N)
230  IF T < 16 THEN 270
240  GOSUB 8000
250  GOSUB 9000
260  T=0
270  NEXT
280  PRINT
290  PRINT"ARE THESE ANSWERS CORRECT";
300  INPUT X$
310  IF LEFT$(X$,1) = "N" THEN 100
400  REM *INPUT STUDENT'S ANSWERS*
410  GOSUB 9000
420  C = 0
430  W = 0
440  S=S+1
450  PRINT"TYPE ANSWERS FOR STUDENT #";S
460  PRINT
470  FOR N = 1 TO Q
480  PRINT"QUESTION #";N;" ";
490  INPUT A
500  IF A = A(N) THEN GOSUB 2000
510  IF A <> A(N) THEN GOSUB 3000
520  NEXT
530  GOSUB 1000
535  PRINT
540  PRINT"ANY MORE STUDENTS (Y/N) ";
550  INPUT X$
560  IF LEFT$(X$,1) = "Y" THEN 410
600  REM *GROUP AVERAGES*
610  GOSUB 9000
620  FOR N = 1 TO S
630  V=V+R(N,1)
640  P=P+R(N,3)
660  NEXT
670  GOSUB 7000
680  PRINT"G R O U P    A V E R A G E S"
690  GOSUB 7000
700  PRINT
705  PRINT
710  PRINT
715  PRINT
720  PRINT"NO. OF STUDENTS:    ";S
730  PRINT
740  PRINT"NO. OF QUESTIONS:   ";Q
750  PRINT
760  PRINT"GROUP AVERAGE:      ";INT(V/S)
770  PRINT
780  PRINT"AVERAGE PERCENT:    ";INT(P/S);"%"
790  PRINT
800  PRINT
810  PRINT
840  GOSUB 8000
870  CLS
880  RUN
1000 REM * INDIVIDUAL RESULTS*
1010 R(S,1) = C
1020 R(S,2) = W
1030 R(S,3) = INT(C*100/Q)
1090 GOSUB 9000
1095 GOSUB 7000
1100 PRINT"RESULTS FOR STUDENT #";S
1105 GOSUB 7000
1108 PRINT
1110 PRINT
1112 PRINT
1113 PRINT"NO. OF QUESTIONS:     ";Q
1115 PRINT
1120 PRINT"QUESTIONS CORRECT:    ";R(S,1)
1130 PRINT
1140 PRINT"QUESTIONS WRONG:      ";R(S,2)
1150 PRINT
1160 PRINT"PERCENTAGE CORRECT:   ";R(S,3);"%"
1170 GOSUB 8500
1300 RETURN
2000 REM *CORRECT ROUTINE*
2010 C=C+1
2030 RETURN
3000 REM *WRONG ROUTINE*
3010 W=W+1
3030 RETURN
4000 REM *SETTING UP*
4010 GOSUB 9000
4020 GOSUB 7000
4030 PRINT"   T E S T    M A R K E R"
4040 GOSUB 7000
4050 PRINT
4055 PRINT
4060 PRINT"THIS PROGRAM WILL MARK AND"
4070 PRINT
4080 PRINT"GIVE RESULTS FOR UP TO 50"
4090 PRINT
4100 PRINT"STUDENTS.  ALL ANSWERS MUST"
4110 PRINT
4120 PRINT"BE NUMERIC."
4130 GOSUB 8500
4140 PRINT
4150 PRINT
4160 PRINT"HOW MANY QUESTIONS";
4170 INPUT Q
4180 DIM A(Q)
4190 DIM R(50,3)
4200 RETURN
7000 REM *LINE ROUTINE*
7010 PRINT"****************************"
7020 RETURN
8000 REM *PAUSE ROUTINE*
8010 PRINT
8020 PRINT
8025 GOSUB 7000
8030 PRINT"MOVE JOYSTICK TO CONTINUE"
8035 GOSUB 7000
8040 IF JOY(1) > 0 THEN RETURN
8050 IF JOY(2) > 0 THEN RETURN
8060 GOTO 8040
8500 REM *MOVE PRINTS UP*
8510 FOR N = 1 TO 8
8520 PRINT
8530 NEXT
8540 RETURN
9000 REM *SET COLORS*
9010 CLS
9015 COLOR 0,15
9020 FOR X = 5 TO 12
9030 COLOR X,2,15
9040 NEXT
9050 COLOR 6,7,15
9070 RETURN
