10  IF  PEEK (116) <  > 176 THEN  POKE 116,176: RUN 
20  DIM H$(63)
30 EX = 2: IF  PEEK (45228) <  > 2 THEN  POKE 45228,0:EX = 0
40 CLS$ =  CHR$ (5) +  CHR$ (13): FOR I = 0 TO 23:CLS$ = CLS$ +  CHR$ (5): NEXT I:R$ =  CHR$ (24)
50  FOR I = 1 TO 15:P$ = P$ +  CHR$ (8):C$ = C$ + R$: NEXT I:H$ =  CHR$ (5)
60  FOR I = 1 TO 37:W$ = W$ +  CHR$ (94): NEXT I
90  GOSUB 900
190 Z = 0:PX = 0:PY = 0: GOTO 300
200  CALL 45279: PRINT CLS$R$R$R$"  :"Z" "R$: VPOKE 47226,Z:A = 45312 + Z * 8: FOR I = 0 TO 7:B =  PEEK (A + I): PRINT "."H$( INT (B / 4));: IF EX THEN  PRINT EX$(B -  INT (B / 4) * 4);
210  PRINT : NEXT I: RETURN 
300  GOSUB 200: COLOR= 2,1
310  PRINT CLS$C$ CHR$ (21);: FOR I = 0 TO PX: PRINT  CHR$ (8);: NEXT I: FOR I = 0 TO PY: PRINT  CHR$ (24);: NEXT I
350  GET K$: SOUND (242,1,15)
360  IF K$ = "T" THEN 1200
370  IF K$ = "L" THEN 1300
380  IF K$ = "U" THEN  GOSUB 1500: GOTO 300
400  IF K$ = "*" THEN  FOR I = 0 TO 15: SOUND (200,1,15): SOUND (210,1,15),(220,1,15): SOUND (1,1,0): NEXT I: GET K$: IF K$ = "*" THEN  CALL 45229: CALL 45279
410  IF K$ =  CHR$ (24) AND PY < 7 THEN Z = Z + 32:PY = PY + 1: PRINT K$;: GOTO 350
415  IF K$ =  CHR$ (24) AND PY = 7 THEN Z = Z - 224:PY = 0: PRINT H$H$H$H$H$H$H$;
420  IF K$ =  CHR$ (5) AND PY > 0 THEN Z = Z - 32:PY = PY - 1: PRINT K$;: GOTO 350
425  IF K$ =  CHR$ (5) AND PY = 0 THEN Z = Z + 224:PY = 7: PRINT R$R$R$R$R$R$R$;
430  IF K$ =  CHR$ (21) AND PX > 0 THEN Z = Z - 1:PX = PX - 1: PRINT K$;: GOTO 350
435  IF K$ =  CHR$ (21) AND PX = 0 THEN Z = Z + 31:PX = 31: PRINT P$P$ CHR$ (8);
440  IF K$ =  CHR$ (8) AND PX < 31 THEN Z = Z + 1:PX = PX + 1: PRINT K$;: GOTO 350
445  IF K$ =  CHR$ (8) AND PX = 31 THEN Z = Z - 31:PX = 0: PRINT H$
450  IF K$ = "P" THEN  HOME : PRINT : PRINT " *** OHJELMAN LOPPU ***": END 
460  IF K$ = "E" THEN 500
470  IF  PEEK (830) = 191 OR  PEEK (830) = 187 THEN 300
480  IF K$ = "K" THEN ZZ =  VPEEK (14459): FOR I = 0 TO 7: POKE 45312 + Z * 8 + I, PEEK (45312 + ZZ * 8 + I): NEXT I: GOSUB 200: GOTO 300
485  IF K$ = "I" THEN AA =  INT (Z / 32) * 256 + 45312: FOR I = 0 TO 255: POKE AA + I,252 -  INT ( PEEK (AA + I) / 4) * 4: NEXT I: GOTO 300
490  GOTO 350
500  COLOR= 6,1:A = 45312 + Z * 8: FOR I = 0 TO 7:BU(I) =  PEEK (A + I): NEXT I
510  GOSUB 200: FOR I = 0 TO 7:B =  PEEK (A + I): FOR K = 0 TO 5 + EX:B(I,K) = 0: IF B >  = E(K) THEN B(I,K) = 1:B = B - E(K)
520  NEXT K: NEXT I
540  PRINT CLS$ LEFT$ (C$,Y + 5) LEFT$ (P$,X + 1);
550  CALL 45279: GET K$: SOUND (230 - ((K$ = "Z") * 30),1,15)
560  IF K$ =  CHR$ (24) AND Y < 7 THEN Y = Y + 1: PRINT K$;: GOTO 550
570  IF K$ =  CHR$ (5) AND Y > 0 THEN Y = Y - 1: PRINT K$;: GOTO 550
580  IF K$ =  CHR$ (21) AND X > 0 THEN X = X - 1: PRINT K$;: GOTO 550
590  IF K$ =  CHR$ (8) AND X < 5 + EX THEN X = X + 1: PRINT K$;: GOTO 550
600  IF K$ = "Z" AND B(Y,X) = 1 THEN B(Y,X) = 0: POKE A + Y, PEEK (A + Y) - E(X): PRINT " " CHR$ (21);:K$ = "C": IF X < 5 + EX THEN K$ =  CHR$ (8): GOTO 590
610  IF K$ = "Z" AND B(Y,X) = 0 THEN B(Y,X) = 1: POKE A + Y, PEEK (A + Y) + E(X): PRINT "O" CHR$ (21);: IF X < 5 + EX THEN K$ =  CHR$ (8): GOTO 590
620  IF K$ =  CHR$ (24) AND PY < 7 THEN Z = Z + 32:PY = PY + 1:Y = 0: GOTO 500
630  IF K$ =  CHR$ (5) AND PY > 0 THEN Z = Z - 32:PY = PY - 1:Y = 7: GOTO 500
640  IF K$ =  CHR$ (21) AND PX > 0 THEN Z = Z - 1:PX = PX - 1:X = 5 + EX: GOTO 500
650  IF K$ =  CHR$ (8) AND PX < 31 THEN Z = Z + 1:PX = PX + 1:X = 0: GOTO 500
690  IF K$ = "M" THEN 300
700  IF K$ = "1" THEN B =  PEEK (A): FOR I = 0 TO 6: POKE A + I, PEEK (A + I + 1): NEXT I: POKE A + 7,B: GOTO 510
710  IF K$ = "2" THEN B =  PEEK (A + 7): FOR I = 7 TO 1 STEP  - 1: POKE A + I, PEEK (A + I - 1): NEXT I: POKE A,B: GOTO 510
720  IF K$ = "3" AND EX = 0 THEN  FOR I = 0 TO 7: POKE A + I, INT ( PEEK (A + I) / 4) * 8 - 252 * B(I,0): NEXT I: GOTO 510
725  IF K$ = "3" AND EX THEN  FOR I = 0 TO 7: POKE A + I, PEEK (A + I) * 2 - 255 * B(I,0): NEXT I: GOTO 510
730  IF K$ = "4" AND EX = 0 THEN  FOR I = 0 TO 7: POKE A + I, INT ( PEEK (A + I) / 2) + 128 * B(I,5): NEXT I: GOTO 510
735  IF K$ = "4" AND EX THEN  FOR I = 0 TO 7: POKE A + I, INT ( PEEK (A + I) / 2) + 128 * B(I,7): NEXT I: GOTO 510
740  IF K$ = "5" THEN  FOR I = 0 TO 7: POKE A + I,255 -  PEEK (A + I): NEXT I: GOTO 510
750  IF K$ = "6" THEN  FOR I = 0 TO 3:B =  PEEK (A + I): POKE A + I, PEEK (A + 7 - I): POKE A + 7 - I,B: NEXT I: GOTO 510
760  IF K$ = "7" THEN  FOR I = 0 TO 7:B = 0: FOR K = 0 TO 5 + EX:B = B + E(5 + EX + K) * B(I,K): NEXT K: POKE A + I,B: NEXT I: GOTO 510
770  IF K$ = "8" THEN  FOR I = 0 TO 7: POKE A + I,BU(I): NEXT I: GOTO 510
780  IF K$ = "9" THEN  FOR I = 0 TO 7: POKE A + I,0: NEXT I: GOTO 510
890  GOTO 550
900  FOR I = 0 TO 15: READ H$(I): NEXT I
910  FOR I = 16 TO 31:H$(I) = " O" + H$(I - 16): NEXT I
920  FOR I = 32 TO 47:H$(I) = "O " + H$(I - 32): NEXT I
930  FOR I = 48 TO 63:H$(I) = "OO" + H$(I - 48): NEXT I
940  FOR I = 0 TO 15:H$(I) = "  " + H$(I): NEXT I
950  DATA  "    ","   O","  O ","  OO"
960  DATA  " O  "," O O"," OO "," OOO"
970  DATA  "O   ","O  O","O O ","O OO"
980  DATA  "OO  ","OO O","OOO ","OOOO"
990  FOR I = 0 TO 7: READ E(I): NEXT I: DATA  128,64,32,16,8,4,2,1
1000  COLOR= 2,1: HOME : PRINT : HOME : PRINT  CHR$ (5)"MERKKI-EDITOR      V 3.2": PRINT "=================================="R$
1010  GOSUB 1550
1020  PRINT CLS$R$R$P$"   VALITSE :": FOR I = 1 TO 9: READ H0$,B0$,H1$,B1$: PRINT  LEFT$ (P$,10)H0$":"B0$: PRINT  CHR$ (5) LEFT$ (P$,9)P$H1$":"B1$;
1030  IF  LEN (B1$) < 10 THEN  PRINT : IF I = 3 THEN  PRINT  LEFT$ (P$,10)"U:MERKIN LEVEYS 6/8?": PRINT : PRINT P$"  EDITOINTI  :"
1040  NEXT I
1050  DATA  A,N[YTT\,E,EDITOINTI,I,K[[NTEINEN,K,KOPIONTI
1060  DATA  T,TALLENNUS,L,LATAUS
1070  DATA  1,TL\S,2,ALAS,3,VASEMMALLE
1080  DATA  4,OIKEALLE,5,K[[TEINEN,6,YMP[RI
1090  DATA  7,PEILIKUVA,8,VANHA,9,POISTO
1100  DATA  Z,"PISTE",**,"ENTINEN   ",M,MENU
1110  FOR I = 0 TO 3: READ EX$(I): NEXT I: DATA  "  "," O","O ","OO"
1120  PRINT CLS$C$"----------------------------------";
1130 A = 47745:Z = 0: FOR I = 0 TO 255: VPOKE A,I:Z = Z + 1:A = A + 1: IF Z = 32 THEN A = A + 8:Z = 0
1140  NEXT I
1170  SOUND (200,2,15): SOUND (215,3,15): IF  PEEK (45229) = 169 AND  PEEK (45231) = 133 THEN  RETURN 
1180  FOR I = 0 TO 49: READ B: POKE 45229 + I,B: NEXT I: CALL 45229: SOUND (230,3,15): RETURN 
1190  DATA  169,0,133,224,133,226,169,177,133,225,169,244,133,227,160,0,177,226,145,224,200,208,249,230,225,230,227,165,227,201,252,208,237,162,32,189,85,234,157,223,176,202,16,247,169,177,141,235,176,96
1200  HOME : COLOR= 5,1: PRINT "MERKKITIEDOSTON TALLETUS": CALL 6670
1210  PRINT : PRINT "ONKO NAUHURI                        VALMIINA (K/E) ? ";
1220  GET K$: IF K$ = "E" THEN  RUN 
1230  IF K$ <  > "K" THEN 1220
1250  BSAVE $B0DF,$820
1260  POKE 216,0
1290  RUN 
1300  HOME : COLOR= 5,1: PRINT "MERKKITIEDOSTON TALLENNUS": CALL 6670: PRINT "=================================="
1310  PRINT : PRINT "ONKO NAUHURI                        VALMIINA (K/E) ? ";
1320  GET K$: IF K$ = "E" THEN  RUN 
1330  IF K$ <  > "K" THEN 1220
1350  BLOAD 
1360  POKE 216,0
1390  RUN 
1500  SOUND (100,2,15)
1510  PRINT CLS$C$H$;"6/8 BIT ?";
1520  GET K$: IF K$ = "6" OR K$ = "8" THEN EX =  VAL (K$) - 6: POKE 45228,EX
1530  PRINT : PRINT H$"         "
1540  PRINT CLS$R$R$R$R$
1550  IF EX = 0 THEN  PRINT "........  ": FOR I = 1 TO 8: PRINT ".      .  ": NEXT I: PRINT "........  "
1560  IF EX = 2 THEN  PRINT "..........": FOR I = 1 TO 8: PRINT ".        .": NEXT I: PRINT ".........."
1590  RETURN 
